! ***** EXEMPLO 2 - Pórtico Plano *****

/NOPR
/PMETH,OFF
KEYW,PR_SET,1
KEYW,PR_STRUC,1

/INPUT,variavel_portico,txt,C:\Users\Paulo\Desktop\Programa_Paulo\,

/PREP7

ET,1,BEAM 3     ! TIPO DE ELEMENTO (VIGA 2D)
ET,2,COMBIN40  ! TIPO DE ELEMENTO (MOLA ROTACIONAL)
KEYOPT,2,3,6

!*** PROPRIEDADES GEOMÉTRICAS DA SEÇÃO DA VIGA ***
R,1,a1,i1,1
R,2,a2,i2,1
R,3,a3,i3,1
R,4,a4,i4,1

!*** PROPRIEDADES RIGIDEZ DA LIGAÇÃO ***
R,5,s1,0,0,0,1000,0 
R,6,s2,0,0,0,1000,0   

!*** PROPRIEDADES DO MATERIAL ***
MP,EX,1,206000000 !(MÓDULO DE ELASTICIDADE)
MP,PRXY,1,0.3     ! COEFICIENTE DE POISSON

!*** DEFINIÇÃO DOS NÓS ***
! Colunas da esquerda
K,1,0,0,0,
K,2,0,1.825,0,
K,3,0,3.650,0,
K,4,0,5.475,0,
K,5,0,7.300,0,
! Colunas da direita
K,9,7.300,7.300,0,
K,10,7.300,5.475,0,
K,11,7.300,3.650,0,
K,12,7.300,1.825,0,
K,13,7.300,0,0,
! Vigas do 2º andar
K,17,0,7.300,0,
K,6,1.825,7.300,0,
K,7,3.650,7.300,0,
K,8,5.475,7.300,0,
K,18,7.300,7.300,0,
! Vigas do 1º andar
K,19,0,3.650,0,
K,14,1.825,3.650,0,
K,15,3.650,3.650,0,
K,16,5.475,3.650,0,
K,20,7.300,3.650,0,

! *** DEFINIÇÃO DOS ELEMENTOS ***
! PILARES DO GRUPO 1
LSTR,1,2
LSTR,2,3
LSTR,13,12
LSTR,12,11

! PILARES DO GRUPO 2
LSTR,3,4
LSTR,4,5
LSTR,11,10
LSTR,10,9

! VIGA DO GRUPO 3
LSTR,17,6
LSTR,6,7
LSTR,7,8
LSTR,8,18

! VIGA DO GRUPO 4
LSTR,19,14
LSTR,14,15
LSTR,15,16
LSTR,16,20

! *** DEFINIÇÃO DOS APOIOS ***
DK,1,all
DK,13,all

! *** DISCRETIZAÇÃO DO MODELO ***

! Pilares do térreo            
TYPE,1 
Real,1                    
! Grupo Pilar 1  
LSEL,S,,,1,2,1                   
LESIZE,ALL,,,1  
LMESH,ALL,1 
LSEL,S,,,3,4,1  
LESIZE,ALL,,,1  
LMESH,ALL,1    

! Pilares do 1º Pavimento
TYPE,1               
Real,2
LSEL,S,,,5,6,1                   
LESIZE,ALL,,,1  
LMESH,ALL,1 
LSEL,S,,,7,8,1  
LESIZE,ALL,,,1  
LMESH,ALL,1 

! Vigas do 2º andar
TYPE,1                           
Real,3
LSEL,S,,,9,12,1      
LESIZE,ALL,,,1  
LMESH,ALL,1 

! Vigas do 1º andar
TYPE,1 
Real,4                            
LSEL,S,,,13,16,1  
LESIZE,ALL,,,1  
LMESH,ALL,1 

! Molas Rotacionais
TYPE,2
REAL,6
E,3,16
E,6,20

! Molas Rotacionais
TYPE,2
REAL,5
E,8,11
E,10,15

!******************** ACOPLA OS NOS ********************
CP,1,UX,3,16
CP,2,UX,6,20
CP,3,UX,8,11
CP,4,UX,10,15
CP,5,UY,3,16
CP,6,UY,6,20
CP,7,UY,8,11
CP,8,UY,10,15



! *** CARREGAMENTO ***
ESEL,S,REAL,,4                             ! seleciona os elementos relacionados a RC 1
SFBEAM,all,1,PRES,62.5                     ! aplica carga distribuida nos elementos selecionados
ESEL,S,REAL,,3
SFBEAM,all,1,PRES,38
ALLSEL                                     ! seleciona todos os elementos novamente

FK,3,FX,36                              ! aplica força concentrada no kp 2
FK,5,FX,18                              ! aplica força concentrada no kp 3

FINISH
/SOLU

! Analise não linear
ANTYPE,0
NLGEOM,ON
NSUBST,10,,,

SOLVE

FINISH
/POST1

PRRSOL,                               ! LISTA AS REAÇÕES DOS APOIOS

ETABLE,NXI,SMISC,1                   ! DEFINE 'ETABLE' PARA ESFORÇO NORMAL NO NÓ “I”	1
ETABLE,NX1,SMISC,7                   ! DEFINE 'ETABLE' PARA ESFORÇO NORMAL NO NÓ “J”    7
ETABLE,QYI,SMISC,2                   ! DEFINE 'ETABLE' PARA ESFORÇO CORTANTE NO NÓ “I”	2
ETABLE,QY1,SMISC,8                   ! DEFINE 'ETABLE' PARA ESFORÇO CORTANTE NO NÓ “J”  8
ETABLE,MomFi,SMISC,6                 ! DEFINE 'ETABLE' PARA MOMENTO FLETOR NO NÓ “I”    6
ETABLE,MomFj,SMISC,12                ! DEFINE 'ETABLE' PARA MOMENTO FLETOR NO NÓ "j"   12
ETABLE,UX,U,X
ETABLE,UY,U,Y
PRETAB,NXI,NX1,QYI,QY1,MomFi,MomFj,UX,UY





